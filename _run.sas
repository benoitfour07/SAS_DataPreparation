/* initial setup *********************************************************/
ods exclude all; /* disable heavy JavaScript outputs */
options mprint; /* display log that are generated by macro execution */
options compress=binary; /* compress sas dataset (to make the run-time shorter) */
%let MAIN_DIR = \\srvhfile1\ShareFiles\Actuarial\DD\Tech_Pricing\working_2018\exercise3_2;
%let MACRO_DIR = \\srvhfile1\ShareFiles\Actuarial\DD\Tech_Pricing\working_2018\sas_macro;
%include "&MACRO_DIR.\macro_common.sas";
%include "&MACRO_DIR.\macro_motor.sas"; /* or macro_pet.sas */

data _null_ ;
  call symputx('EXC_DAY', put(today(), yymmddn.)) ;
  call symputx('EXC_TIME', tranwrd(substr(put(datetime(), datetime.), 9, 5), ":", "-")) ;
run ;

%let LOG_FILE_NAME = &MAIN_DIR.\saslog\LOG_&EXC_DAY.-&EXC_TIME..log;
%let LOG_CHACK = &MAIN_DIR.\saslog\LOG_&EXC_DAY.-&EXC_TIME._CHECK.txt;

proc printto log="&LOG_FILE_NAME." new; /* output log as a text file */
run; 



/* get base data *********************************************************/
libname hist "\\srvhfile1\ShareFiles\Technical\Adj\Technical\Common.Technical\Macro_Reporting\Monthly_report\data";

data cont_org;
	set hist.cn180331;
run;

%ini_policy(INPUT=cont_org, OUTPUT=cont_org);
%calc_trans_period(INPUT=cont_org, OUTPUT=cont_org);
%rem_ineffective_pol(INPUT=cont_org, OUTPUT=cont_org);

data claim_org;
	set hist.cl180331;
run;

%ini_claim(INPUT=claim_org, OUTPUT=claim_org);
%policy_version_correction(INPUT_CLM=claim_org, OUTPUT_CLM=claim_org, INPUT_CNT=cont_org, TRN_BEG_V=TRN_BEG, TRN_END_V=TRN_END);



/* calculate exposure and earned premium *********************************************************/
/* reduce the data size for quicker run */
data cont;
	set cont_org (where=(TARCODE in ("", "00001"))); /* extract only auto data */

	if "01Jan2016"d <= DTINC_CO < "01Jan2018"d;

	keep
		POL_NUM
		NUMBER
		TARCODE
		DTINC_CO
		DTEXP_CO
		TRN_BEG
		TRN_END
		AIP_:
		;

run;

%macro calc_earned(PERIL, PREM);

	EX_&PERIL. = ifn(&PREM. > 0, EX, 0);	
	EP_&PERIL. = EX * &PREM.;	

%mend;

data cont;
	set cont;

	format
		LOB $12.
		LP_BEG yymmdd10.
		LP_END yymmdd10.
		ACC_YEAR best12.
		;

	select(TARCODE);
		when("", "00001")	LOB = "AUTO";
		when("00004")	LOB = "BIKE";
		when("00005")	LOB = "MOPED";
		otherwise 		LOB = "_NA";
	end;

	/* duplicate transactions */
	do N = 1 to 13;

		LP_BEG = intnx("month", TRN_BEG, N - 1, "beginning");
		LP_END = intnx("month", TRN_BEG, N, "beginning");

		ACC_YEAR = year(LP_BEG);
		ACC_MONTH = month(LP_BEG);
		
		EX = (min(LP_END, TRN_END) - max(LP_BEG, TRN_BEG)) / (DTEXP_CO - DTINC_CO);

		%calc_earned(BI, AIP_BIL);
		%calc_earned(PD, AIP_DL);
		%calc_earned(PA, AIP_PA);
		%calc_earned(PI, AIP_PI);

		if "01Jan2017"d <= LP_BEG < min("01Jan2018"d, TRN_END) then output;

	end;

run;



/* aggregate claim info by transaction, accident year and month *********************************************************/
%macro getClaimInfo(PERIL, CL_COND);
	sum(ifn(&CL_COND. and INC > 0, 1, 0)) as CLM_&PERIL.,
	sum(ifn(&CL_COND., INC, 0)) as INC_&PERIL.
%mend;

proc sql ;
	create table claim as
	select
		year(EV_DTOCC) as ACC_YEAR,
		month(EV_DTOCC) as ACC_MONTH,
		POL_NUM,
		NUMBER,
		%getClaimInfo(BI, PBB_NBR eq 1),
		%getClaimInfo(PD, PBB_NBR eq 2),
		%getClaimInfo(PA, PBB_NBR eq 3),
		%getClaimInfo(PI, PBB_NBR eq 7)
	from claim_org
	where "01Jan2017"d <= EV_DTOCC < "01Jan2018"d
	group by
		ACC_YEAR,
		ACC_MONTH,
		POL_NUM,
		NUMBER
	order by
		ACC_YEAR,
		ACC_MONTH,
		POL_NUM,
		NUMBER
		;
quit;



/* merge *********************************************************/
proc sort data=cont tagsort noequals;
	by ACC_YEAR ACC_MONTH POL_NUM NUMBER;
run;

data cnt_clm;
	merge cont(in=d) claim;
	by ACC_YEAR ACC_MONTH POL_NUM NUMBER;
	if d;
run;



proc summary data=cnt_clm nway;
	class ACC_YEAR ACC_MONTH;
	var EX_: EP_: CLM_: INC_:;
	output out=output(drop=_TYPE_ _FREQ_) sum()=;
run;

%macro calcFreqAndLR(PERIL);
	format FREQ_&PERIL. percent10.2;
	format LR_&PERIL. percent10.2;
	FREQ_&PERIL. = CLM_&PERIL. / EX_&PERIL.;
	LR_&PERIL. = INC_&PERIL. / EP_&PERIL.;
%mend;

data output(drop=CLM_: INC_:);
	set output;

	%calcFreqAndLR(BI);
	%calcFreqAndLR(PD);
	%calcFreqAndLR(PA);
	%calcFreqAndLR(PI);

run;



/*  SAS output *********************************************************/
libname out "&MAIN_DIR.";

data out.output;
	set output;
run;



/*  CSV output *********************************************************/
proc export
	data=output
	outfile="&MAIN_DIR.\output.csv"
	dbms=csv
	replace;
run;



/* check log *********************************************************/
proc printto; /* stop outputting log as a text file */
run;

%check_log_jp(SASLOG="&LOG_FILE_NAME.", OUTPUT="&LOG_CHACK.");
*%check_log_en(SASLOG="&LOG_FILE_NAME.", OUTPUT="&LOG_CHACK."); 
